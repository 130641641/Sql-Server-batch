''----------------------------------------------------------------------------------------
''--
''-- テーブルの更新
''--
''-- 2000.11.09 作成
''--
''----------------------------------------------------------------------------------------
Option Explicit
On Error Resume Next

Dim objShell    ''-- Shell

Dim oParam      ''-- パラメータ
Dim objConn     ''-- ADO Connector
Dim objRS       ''-- ADO RecordSet

Dim ConnStr     ''-- 接続文字列
Dim SERVER      ''-- 接続サーバ
Dim DBS         ''-- 接続DB
Dim UID         ''-- 接続User Id
Dim PWD         ''-- Password

Dim OLDConnStr  ''-- 接続文字列
Dim OLDSERVER   ''-- 接続サーバ
Dim OLDDBS      ''-- 接続DB
Dim OLDUID      ''-- 接続User Id
Dim OLDPWD      ''-- Password

Dim sqlStr      ''-- SQL構文
Dim sqlStr2     ''-- SQL構文
Dim DtCnt
Dim setValue
Dim CsvPath     ''-- CSVファイルパス
Dim CsvPath2    ''-- CSVファイルパス(BULK INSERT)

Dim myArrayList ''-- 更新SQLリスト

Dim objFSO      ''-- FileSystemObject
Dim lineStr, spass, wkStr


Set oParam = WScript.Arguments

''-- 引数確認 --
If oParam.Count < 5 then
   WScript.Echo "パラメータエラー"
   WScript.Echo "プログラム名 :SM_TOKUSHU_USER.vbs"
   WScript.Echo "  第1パラメータ:接続サーバ"
   WScript.Echo "  第2パラメータ:接続DB"
   WScript.Echo "  第3パラメータ:UserID"
   WScript.Echo "  第4パラメータ:Password"
   WScript.Echo "  第5パラメータ:CSVファイルパス"
   WScript.Quit
End If

''-- 引数セット
SERVER     = oParam(0)
DBS        = oParam(1)
UID        = oParam(2)
PWD        = oParam(3)
CsvPath    = oParam(4)
OLDSERVER  = oParam(5)
OLDDBS     = oParam(6)
OLDUID     = oParam(7)
OLDPWD     = oParam(8)

''-- CSVファイルの存在確認
Set objFSO = CreateObject("Scripting.FileSystemObject")
If objFSO.FileExists(CsvPath) = False Then
   WScript.Echo "指定CSVファイル [" & CsvPath & "] が見つかりません。" & vbcrlf
   WScript.Quit
Else
   ''-- 参照ファイルの絶対パスを取得
   CsvPath2 = objFSO.GetAbsolutePathName(CsvPath)
End If
Set objFSO = Nothing

Set objShell = CreateObject("WScript.Shell")
''-- 移行元データ抽出
sqlStr2 = chr(34) & "SELECT ADMUSEID,ADMPASS,ADMPASSDATE,ADMPUSER,ADMNAME_F+ADMNAME_L,ADMMAIL,REGDATE FROM RK_OPEN_DB.dbo.T_ADMIN" & chr(34)
sqlStr = "bcp " & sqlStr2 & " queryout " & chr(34) & CsvPath2 & chr(34) & " -c -t~~~ -r@@\n -S " & OLDSERVER & " -U " & OLDUID & " -P " & OLDPWD
Dim strResult
strResult = objShell.Run(sqlStr, 0, True)

If Err.Number <> 0 Then
   WScript.Echo sqlStr
   WScript.Echo "エラー：[" & Err.Number & "] " & Err.Description
   WScript.Quit
End If
Set objShell = Nothing

Set objFSO = WScript.CreateObject("Scripting.FileSystemObject")

''-- 読み込みファイルの指定 (相対パスなのでこのスクリプトと同じフォルダに置いておくこと)
Dim inputFile
Set inputFile = objFSO.OpenTextFile(CsvPath2, 1, False, 0)

''-- 書き出しファイルの指定 (今回は新規作成する)
Dim outputFile
Set outputFile = objFSO.OpenTextFile(CsvPath2 & "_1", 2, True)

Dim StrLines
DtCnt = 0
''-- 読み込みファイルから1行ずつ読み込み、書き出しファイルに書き出すのを最終行まで繰り返す
Do Until inputFile.AtEndOfStream
  lineStr = inputFile.ReadLine
  StrLines = Split( lineStr, "~~~" )
  wkStr = StrLines(1)
  
  ''-- 暗号化
  StrLines(1) = EncodeOn( wkStr )
  outputFile.WriteLine Join( StrLines, "~~~" )
  DtCnt = DtCnt + 1
Loop

''-- バッファを Flush してファイルを閉じる
inputFile.Close
outputFile.Close

''-- 処理後のファイルをコピーする
objFso.CopyFile CsvPath2 & "_1", CsvPath2
Set objFSO = Nothing

WScript.Echo vbcrlf & "抽出件数:" & DtCnt & vbcrlf

''-- 接続文字列準備
ConnStr = "Provider=sqloledb;Data Source=" & SERVER & ";" & _
          "Initial Catalog=" & DBS & ";" & _
          "User ID=" & UID & ";" & _
          "Password=" & PWD & ";"

''-- データベース接続
Set objConn = CreateObject("ADODB.Connection")

objConn.ConnectionString = ConnStr
objConn.Open
objConn.CursorLocation = 3 ' クライアントサイドカーソルに変更

''-- エラー確認
If Err.Number <> 0 then
   WScript.Echo "エラー：[" & Err.Number & "] " & Err.Description
   WScript.Echo "接続[NG]" & vbcrlf
   WScript.Quit
Else
   WScript.Echo "接続[OK]" & vbcrlf
End If

''-- 接続先DBを指定
sqlStr = "USE " & DBS
objConn.Execute sqlStr
If Err.Number <> 0 Then
   WScript.Echo "エラー：[" & Err.Number & "] " & Err.Description
   WScript.Quit
End If

''-- 中間テーブルの処理(存在した場合は消す) --
sqlStr = "IF OBJECT_ID(N'tempdb..##SM_TOKUSHU_USER', N'U') IS NOT NULL" & vbcrlf & _
         "DROP TABLE ##SM_TOKUSHU_USER"
objConn.Execute sqlStr
If Err.Number <> 0 Then
   WScript.Echo "エラー：[" & Err.Number & "] " & Err.Description
   WScript.Quit
End If

''-- 中間テーブル作成 --
sqlStr = "CREATE TABLE ##SM_TOKUSHU_USER (" & _
         "UserID varchar(10)," & _
         "PassCD varchar(10)," & _
         "PassHenkoDT datetime," & _
         "PassHenkoUserID varchar(10)," & _
         "UserKanjiNAME varchar(30)," & _
         "MailAdrTXT varchar(50)," & _  
         "TorokuDT datetime," & _               
         ")"
objConn.Execute sqlStr
If Err.Number <> 0 Then
   WScript.Echo "エラー：[" & Err.Number & "] " & Err.Description
   WScript.Quit
End If

''-- 中間テーブルへのデータ投入 --
sqlStr = "BULK INSERT ##SM_TOKUSHU_USER from '" & CsvPath2 & "' WITH( fieldterminator = '~~~', rowterminator = '@@\n' )"
objConn.Execute sqlStr
If Err.Number <> 0 Then
   WScript.Echo "エラー：[" & Err.Number & "] " & Err.Description
   WScript.Quit
End If

''-- BULK INSERTしたテーブルから本テーブルを更新する --
sqlStr = "UPDATE rkwebopen1.SM_TOKUSHU_USER " & vbcrlf & _
         "SET UserID=B.UserID" & vbcrlf & _
         "   ,PassCD=B.PassCD " & vbcrlf & _ 
         "   ,PassHenkoUserID=B.PassHenkoUserID " & vbcrlf & _                 
         "   ,PassHenkoDT=B.PassHenkoDT " & vbcrlf & _     
         "   ,UserkanjiNAME=B.UserkanjiNAME " & vbcrlf & _
         "   ,MailAdrTXT=B.MailAdrTXT " & vbcrlf & _ 
         "   ,TorokuDT=B.TorokuDT " & vbcrlf & _  
         "FROM rkwebopen1.SM_TOKUSHU_USER A" & vbcrlf & _
         "     INNER JOIN ##SM_TOKUSHU_USER B" & vbcrlf & _
         "     ON A.UserID=B.UserID "
objConn.Execute sqlStr
If Err.Number <> 0 Then
   WScript.Echo "エラー：[" & Err.Number & "] " & Err.Description
   WScript.Echo sqlStr
   WScript.Quit
End If


Set objRS = CreateObject("ADODB.Recordset")
''-- スイッチング完了時の更新内容 --
sqlStr = "SELECT @@ROWCOUNT"
''-- 検索実行
objRS.Open sqlStr, objConn
''-- エラー確認
If Err.Number <> 0 then
   WScript.Echo "エラー：[" & Err.Number & "] " & Err.Description
   WScript.Echo "検索[NG]" & vbcrlf
   WScript.Quit
End If

Do Until objRS.EOF
     DtCnt = objRS(0).Value
     objRS.MoveNext
Loop

objRS.Close
Set objRS = Nothing
WScript.Echo vbcrlf & "処理件数:" & DtCnt & vbcrlf

objConn.Close
WScript.Echo "切断[OK]"
Set objConn = Nothing

WScript.Quit

' -------------------------------------
' パスワード暗号化　処理 --------------
' -------------------------------------
Function EncodeOn( ByRef TargetStr )

  
  Dim LV_chkStr0
  Dim LV_chkStr1
  Dim LV_newStr2
  Dim i
  
  ''-- 対象文字列ループ
  For i = 1 To len(TargetStr)
      ''-- 1文字読み込み
      LV_chkStr0 = Mid(TargetStr, i, 1)
      
      ''-- 処理開始
      LV_chkStr0 = LCase(LV_chkStr0)                          ' 小文字変換
      If LV_chkStr0 >= "a" and LV_chkStr0 <= "m" Then         '  [a-m]の場合
        LV_chkStr1 = Chr(Asc(LV_chkStr0)+13)                  '    LOT+13
      ElseIf LV_chkStr0 >= "n" and LV_chkStr0 <= "z" Then     '  [n-z]の場合
        LV_chkStr1 = Chr(Asc(LV_chkStr0)-13)                  '    LOT-13
      ElseIf LV_chkStr0 >= "0" and LV_chkStr0 <= "4" Then     '  [0-5]の場合
        LV_chkStr1 = Chr(Asc(LV_chkStr0)+5)                   '    LOT+5
      ElseIf LV_chkStr0 >= "5" and LV_chkStr0 <= "9" Then     '  [6-9]の場合
        LV_chkStr1 = Chr(Asc(LV_chkStr0)-5)                   '    LOT-5
      Else                                                    '  [それ以外]
        LV_chkStr1 = LV_chkStr0                               '    そのまま
      End If
      LV_newStr2 = LV_newStr2 & LV_chkStr1                    ' 変換後文字セット
  Next
    
  ''-- Encode後の結果を戻す
  EncodeOn = LV_newStr2
  
End Function

