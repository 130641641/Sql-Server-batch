@echo off

rem ***************************************************************************
rem システム名   : フォルダアクセス権限の調査
rem モジュール名 : TestFolderAuth.BAT
rem 処理名       : 
rem 注意事項     :
rem
rem Date        EDP-No    所属/氏名                内   容
rem ---------------------------------------------------------------------------
rem 2016/06/15  P-XXXXXX  HANAWA/李                新規作成
rem yyyy/mm/dd  P-XXXXXX  ＃＃＃＃＃＃＃＃         変更
rem ***************************************************************************

rem ---------------- START Of Batch -------------------------------------------
setlocal

rem ===========================================================================
rem 環境変数の設定
rem ---------------------------------------------------------------------------
:USR_PATH
call %~dp0\SET_ENV.BAT

set BAT_ID=%~n0
set STATUS=0

rem ===========================================================================
rem Batch Start 処理
rem ---------------------------------------------------------------------------
:START
set LOGFILE=%LOG_PATH%%BAT_ID%.LOG
set time2=%time: =0%
set LOG_BAK_NAME=%date:~-10,4%%date:~-5,2%%date:~-2,2%%time2:~0,2%%time2:~3,2%%time2:~6,2%

copy /y %LOGFILE% %LOGBK_PATH%%BAT_ID%\%BAT_ID%_%LOG_BAK_NAME%.LOG > nul

del %LOGFILE% 2> nul

echo %LINE01% >> %LOGFILE%
echo %BAT_ID% %BAT_PATH%%BAT_ID%.BAT START >> %LOGFILE%
echo %date% %time% >> %LOGFILE%
echo %LINE02% >> %LOGFILE%

rem ===========================================================================
rem Batch Main 処理
rem ---------------------------------------------------------------------------
echo ログ変数出力 → 開始 >> %LOGFILE%
echo ログファイルパス=%LOGFILE% >> %LOGFILE%
echo バックアップログファイルパス=%LOGBK_PATH%%BAT_ID%_%LOG_BAK_NAME%.LOG >> %LOGFILE%
echo ログ変数出力 → 終了 >> %LOGFILE%

echo %date% %time% %EXE_PATH%%BAT_ID%.exeの起動 → 開始 >> %LOGFILE%
echo 引数１：%~1を設定 >> %LOGFILE%
call %EXE_PATH%%BAT_ID%.exe %~1
set STATUS=%ERRORLEVEL%
echo %date% %time% %EXE_PATH%%BAT_ID%.exeの起動 → 終了(%STATUS%) >> %LOGFILE%
if %STATUS% GEQ 1 GOTO END

rem ===========================================================================
rem Batch End 処理
rem ---------------------------------------------------------------------------
:END
echo %LINE02% >> %LOGFILE%
echo %BAT_ID% %BAT_PATH%%BAT_ID%.BAT END   >> %LOGFILE%
echo %date% %time% >> %LOGFILE%
echo %LINE01% >> %LOGFILE%

rem バッチのエラー終了コードは9になる
if %STATUS% GTR 0 (
	set STATUS=9
)

echo %BAT_ID% 終了コード＝%STATUS% >> %LOGFILE%

exit /b %STATUS%
rem ===========================================================================

rem ---------------- End Of Batch ---------------------------------------------
